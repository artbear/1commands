// #Использовать tempfiles
#Использовать logos

Перем Лог;
Перем ЗапущенныеКоманды;

// Возвращает соответствие для) запущенных команд
//
//  Возвращаемое значение:
//   Соответствие - 
//		* Ключ - Команда - Запущенная команда
//		* Значение - Структура - описание процесса и его свойств
//			* Процесс - Процесс - сам процесс
//			* Свойства - Структура - копия свойств процесса в конце метода выполнения фоновых команд
//				* Завершен - Булево - 
//				* КодВозврата - Число - 
//				* Идентификатор - Число - 
//
Функция ЗапущенныеКоманды() Экспорт
	
	Результат = Новый Соответствие();
	Для каждого КлючЗначение Из ЗапущенныеКоманды Цикл
		Результат.Вставить(КлючЗначение.Ключ, КлючЗначение.Значение);
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

// Запускает команду в фоне
//
// Параметры:
//   Команда - Команда - запускаемая команда
//
//  Возвращаемое значение:
//   Процесс - запущенный процесс
//
Функция ЗапуститьКоманду(Знач Команда) Экспорт
	
	Процесс = Команда.ЗапуститьПроцесс();

	СвойстваПроцесса = Новый Структура;
	СвойстваПроцесса.Вставить("КодировкаВывода", "");
	СвойстваПроцесса.Вставить("Завершен", Ложь);
	СвойстваПроцесса.Вставить("КодВозврата", -100500);
	СвойстваПроцесса.Вставить("Идентификатор", Неопределено);

	ОписаниеПроцесса = Новый Структура("Процесс, Свойства", Процесс, СвойстваПроцесса);

	ЗапущенныеКоманды.Вставить(Команда, ОписаниеПроцесса);

	Лог.Отладка(СтрШаблон("Запускаю фоновую команду ИД %1", Процесс.Идентификатор));

	Возврат Процесс;

КонецФункции

// Ожидать завершения всех подготовленных команд с таймаутом в миллисекундах
//
// Параметры:
//   Таймаут - Число - таймаут в миллисекундах
//
//  Возвращаемое значение:
//   Булево - Все ли процессы завершились или нет
//
Функция ОжидатьЗавершения(Знач Таймаут) Экспорт
	
	Для каждого КлючЗначение Из ЗапущенныеКоманды Цикл
		ОписаниеПроцесса = КлючЗначение.Значение;
		Процесс = ОписаниеПроцесса.Процесс;
		Процесс.ОжидатьЗавершения(Таймаут);
	КонецЦикла;
	
	ДатаНачала = ТекущаяУниверсальнаяДатаВМиллисекундах();
	ДатаОкончания = ДатаНачала + Таймаут;

	Пока ТекущаяУниверсальнаяДатаВМиллисекундах() < ДатаОкончания Цикл
		Для каждого КлючЗначение Из ЗапущенныеКоманды Цикл
			ОписаниеПроцесса = КлючЗначение.Значение;
			Процесс = ОписаниеПроцесса.Процесс;
			Если Не Процесс.Завершен Тогда
			   Процесс.ОжидатьЗавершения(Таймаут);
		   КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Результат = Истина;
	Для каждого КлючЗначение Из ЗапущенныеКоманды Цикл
		ОписаниеПроцесса = КлючЗначение.Значение;
		Процесс = ОписаниеПроцесса.Процесс;
		Если Не Процесс.Завершен Тогда
			Результат = Ложь;
		Иначе
			ЗаполнитьЗначенияСвойств(ОписаниеПроцесса.Свойства, ОписаниеПроцесса.Процесс);
	   КонецЕсли;
	КонецЦикла;

	Возврат Результат;

КонецФункции

// Очистить все команды, неважно, запущены они или нет.
//
Процедура Очистить() Экспорт

	ЗапущенныеКоманды = Новый Соответствие();

КонецПроцедуры

// Имя лога. Возвращается "oscript.lib.commands"
//
//  Возвращаемое значение:
//   Строка - 
//
Функция ИмяЛога() Экспорт
	Возврат "oscript.lib.commands";
КонецФункции

ЗапущенныеКоманды = Новый Соответствие();
Лог = Логирование.ПолучитьЛог(ИмяЛога());
