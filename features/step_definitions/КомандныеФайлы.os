// Реализация шагов BDD-фич/сценариев c помощью фреймворка https://github.com/artbear/1bdd
#Использовать "../.."

Перем БДД; //контекст фреймворка 1bdd
Перем ЭтоWindows;
Перем ПропускаюВЛинукс;

// Метод выдает список шагов, реализованных в данном файле-шагов
Функция ПолучитьСписокШагов(КонтекстФреймворкаBDD) Экспорт
	БДД = КонтекстФреймворкаBDD;

	ВсеШаги = Новый Массив;

	ВсеШаги.Добавить("ЯСоздаюКомандныйФайл");
	ВсеШаги.Добавить("ЯСоздаюКомандныйФайлPowerShell");
	ВсеШаги.Добавить("ЯДобавляюСтрокуВКомандныйФайл");
	ВсеШаги.Добавить("ЯСообщаюСодержимоеКомандногоФайла");
	ВсеШаги.Добавить("ЯВыполняюКомандныйФайл");
	ВсеШаги.Добавить("ВыводКомандногоФайлаСодержит");
	ВсеШаги.Добавить("КодВозвратаКомандногоФайлаРавен");
	ВсеШаги.Добавить("ЯУстанавливаюПриложениеЗапуска");
	ВсеШаги.Добавить("ЯПропускаюЭтотСценарийВЛинукс");

	Возврат ВсеШаги;
КонецФункции

// Реализация шагов

// Процедура выполняется перед запуском каждого сценария
Процедура ПередЗапускомСценария(Знач Узел) Экспорт
	ПропускаюВЛинукс = Ложь;
КонецПроцедуры

// Процедура выполняется после завершения каждого сценария
Процедура ПослеЗапускаСценария(Знач Узел) Экспорт
	
КонецПроцедуры

//Я создаю командный файл
Процедура ЯСоздаюКомандныйФайл() Экспорт
	СоздатьФайл();
КонецПроцедуры

// Я пропускаю этот сценарий в Линукс
Процедура  ЯПропускаюЭтотСценарийВЛинукс() Экспорт
	ПропускаюВЛинукс = Истина;
КонецПроцедуры

//Я создаю командный файл PowerShell ""  ".ps1"
Процедура ЯСоздаюКомандныйФайлPowerShell(Знач Путь="", Знач Расширение="") Экспорт
	Если ПропускаюШагВЛинукс() Тогда
		БДД.ВыполнитьШаг("Пропускаю шаг в Linux");
		Возврат;
	КонецЕсли;

	СоздатьФайл(Путь,Расширение);
КонецПроцедуры

//я добавляю строку "echo командный файл" в командный файл
Процедура ЯДобавляюСтрокуВКомандныйФайл(Знач СтрокаКоманды) Экспорт
	Если ПропускаюШагВЛинукс() Тогда
		БДД.ВыполнитьШаг("Пропускаю шаг в Linux");
		Возврат;
	КонецЕсли;

	КомандныйФайл = БДД.ПолучитьИзКонтекста("КомандныйФайл");

	КомандныйФайл.ДобавитьКоманду(СтрокаКоманды);
КонецПроцедуры

//Я выполняю командный файл
Процедура ЯВыполняюКомандныйФайл() Экспорт
	Если ПропускаюШагВЛинукс() Тогда
		БДД.ВыполнитьШаг("Пропускаю шаг в Linux");
		Возврат;
	КонецЕсли;

	КомандныйФайл = БДД.ПолучитьИзКонтекста("КомандныйФайл");

	КомандныйФайл.Исполнить();
КонецПроцедуры

//Я сообщаю содержимое файла "ИмяФайла"
Процедура ЯСообщаюСодержимоеФайла(Знач ИмяФайла) Экспорт
	Если ПропускаюШагВЛинукс() Тогда
		БДД.ВыполнитьШаг("Пропускаю шаг в Linux");
		Возврат;
	КонецЕсли;

	ВывестиТекстФайла(ИмяФайла);
КонецПроцедуры

//Я сообщаю содержимое командного файла
Процедура ЯСообщаюСодержимоеКомандногоФайла() Экспорт
	Если ПропускаюШагВЛинукс() Тогда
		БДД.ВыполнитьШаг("Пропускаю шаг в Linux");
		Возврат;
	КонецЕсли;
	КомандныйФайл = БДД.ПолучитьИзКонтекста("КомандныйФайл");

	ТекстФайла = КомандныйФайл.ПолучитьТекстФайла();
	Сообщить(ТекстФайла);
КонецПроцедуры

//Вывод командного файла содержит "командный файл"
Процедура ВыводКомандногоФайлаСодержит(Знач ОжидаемыйВыводКоманды) Экспорт
	Если ПропускаюШагВЛинукс() Тогда
		БДД.ВыполнитьШаг("Пропускаю шаг в Linux");
		Возврат;
	КонецЕсли;

	КомандныйФайл = БДД.ПолучитьИзКонтекста("КомандныйФайл");

	ВыводКоманды = КомандныйФайл.ПолучитьВывод();
	Ожидаем.Что(ВыводКоманды).Содержит(ОжидаемыйВыводКоманды);
КонецПроцедуры

//Код возврата командного файла равен 0
Процедура КодВозвратаКомандногоФайлаРавен(Знач ОжидаемыйКодВозврата) Экспорт
	Если ПропускаюШагВЛинукс() Тогда
		БДД.ВыполнитьШаг("Пропускаю шаг в Linux");
		Возврат;
	КонецЕсли;

	КомандныйФайл = БДД.ПолучитьИзКонтекста("КомандныйФайл");

	Ожидаем.Что(КомандныйФайл.ПолучитьКодВозврата(), "Код возврата").Равно(ОжидаемыйКодВозврата);
КонецПроцедуры

//Я устанавливаю приложение запуска "C:\Windows\System32\WindowsPowerShell\v1.0\powershell.exe"
Процедура ЯУстанавливаюПриложениеЗапуска(Знач ПутьПриложения="") Экспорт
	Если ПропускаюШагВЛинукс() Тогда
		БДД.ВыполнитьШаг("Пропускаю шаг в Linux");
		Возврат;
	КонецЕсли;

	КомандныйФайл = БДД.ПолучитьИзКонтекста("КомандныйФайл");
	Если КомандныйФайл=Неопределено Тогда
		КомандныйФайл = Новый КомандныйФайл; 
	КонецЕсли;
	КомандныйФайл.УстановитьПриложение(ПутьПриложения);
	БДД.СохранитьВКонтекст("КомандныйФайл", КомандныйФайл);
КонецПроцедуры


Процедура ВывестиТекстФайла(Знач ИмяФайла, Знач Кодировка = Неопределено)

	Файл = Новый Файл(ИмяФайла);
	Если НЕ Файл.Существует() Тогда
		Возврат;
	КонецЕсли;

	Если Кодировка = Неопределено Тогда
		Кодировка = "utf-8";
	КонецЕсли;

	ЧТ = Новый ЧтениеТекста(ИмяФайла, Кодировка);
	СтрокаФайла = ЧТ.Прочитать();
	ЧТ.Закрыть();

	Сообщить(СтрокаФайла);

КонецПроцедуры

Процедура СоздатьФайл(Знач Путь="", Знач Расширение="")
	КомандныйФайл = БДД.ПолучитьИзКонтекста("КомандныйФайл");
	Если КомандныйФайл=Неопределено Тогда
		КомандныйФайл = Новый КомандныйФайл; 
	КонецЕсли;
	
	КомандныйФайл.Создать(Путь,Расширение);
	БДД.СохранитьВКонтекст("КомандныйФайл", КомандныйФайл);	
КонецПроцедуры

Функция ПропускаюШагВЛинукс()
	Если ЭтоWindows = Неопределено Тогда
		СистемнаяИнформация = Новый СистемнаяИнформация;
		ЭтоWindows = Найти(НРег(СистемнаяИнформация.ВерсияОС), "windows") > 0;
	КонецЕсли;

	Возврат ПропускаюВЛинукс И НЕ ЭтоWindows;
КонецФункции // ЭтоWindows()
